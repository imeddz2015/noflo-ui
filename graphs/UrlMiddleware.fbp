INPORT=Dispatch.IN:IN
OUTPORT=Passed.OUT:PASS
OUTPORT=NewActions.OUT:NEW

'noflo:ready,storage:ready,application:redirect,application:hash' -> ROUTES Dispatch(ui/DispatchAction)
Dispatch PASS -> IN Passed(core/Repeat)

# New actions generated by this middleware
'application:url' -> ACTION ApplicationUrlAction(ui/SetAction)
ApplicationUrlAction OUT -> IN NewActions(core/Merge)
'application:redirect' -> ACTION ApplicationRedirectAction(ui/SetAction)
ApplicationRedirectAction OUT -> IN NewActions

# Application start-up
Dispatch HANDLE[0] -> IN NofloReady(core/Split)
NofloReady OUT -> IN Passed
NofloReady OUT -> IN InitialURL(ui/GetUrl)
# Send application URL out
InitialURL OUT -> ApplicationUrlAction

# Subscribe to hash changes and generate new actions
Dispatch HANDLE[1] -> IN SplitReady(core/Split)
SplitReady OUT -> IN Async(core/RepeatAsync) OUT -> START ListenHash(interaction/ListenHash)
SplitReady OUT -> IN Passed
ListenHash INITIAL -> URL HashToAction(ui/Router)
ListenHash CHANGE -> URL HashToAction
HashToAction ROUTE -> IN NewActions
# Allow router to redirect as well
HashToAction REDIRECT -> IN ApplicationRedirectAction

# URL redirection. Changes URL and as side-effect stops app
Dispatch HANDLE[2] -> IN Redirect(ui/SetUrl)

# URL hash redirection
Dispatch HANDLE[3] -> IN SetUrlHash(ui/SetUrlHash)
